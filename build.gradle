import java.util.function.BiFunction
import java.util.function.Predicate

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.72'
}

version = "2.5.4"
group = "dev.gigaherz.signbutton"

Provider<String> minecraftVersion(Transformer<String, String> mapper) {
    return project.userDevRuntime.runtimes.map(r -> r.values().first().specification.minecraftVersion).map(mapper)
}

base {
    archivesName = minecraftVersion { "SignButton-${it}" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenLocal()
    maven {
        url 'https://maven.tterrag.com/'
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'forge.logging.markers', '' // 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        //ideaModule "${project.name}.main"

        workingDirectory project.file('run')

        //programArguments.addAll "--mixin=jsonthings.mixins.json"

        modSource project.sourceSets.main
    }

    client {
    }

    server {
    }
}

dependencies {
    implementation 'net.neoforged:neoforge:20.4.60-beta'
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "signbutton",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}